group 'SSMByGradle'
version '1.0-SNAPSHOT'
apply plugin: 'war'
//引入generator工具
configurations {
    mybatisGenerator
}
repositories {
    mavenCentral()
}
dependencies {
    //spring核心包
    compile 'org.springframework:spring-core:4.0.2.RELEASE'
    compile 'org.springframework:spring-web:4.0.2.RELEASE'
    compile 'org.springframework:spring-oxm:4.0.2.RELEASE'
    compile 'org.springframework:spring-tx:4.0.2.RELEASE'
    compile 'org.springframework:spring-jdbc:4.0.2.RELEASE'
    compile 'org.springframework:spring-webmvc:4.0.2.RELEASE'
    compile 'org.springframework:spring-aop:4.0.2.RELEASE'
    compile 'org.springframework:spring-context-support:4.0.2.RELEASE'
    compile 'org.springframework:spring-test:4.0.2.RELEASE'
    //mybatis核心包
    compile 'org.mybatis:mybatis:3.2.6'
    //mybatis/spring包
    compile 'org.mybatis:mybatis-spring:1.2.2'
    //java ee jar 包
    compile 'javax:javaee-api:7.0'
    //Mysql数据库链接jar包
    compile 'mysql:mysql-connector-java:5.1.30'
    //dbcp的jar包，用来在applicationContext.xml中配置数据库
    compile 'commons-dbcp:commons-dbcp:1.2.2'
    //JSTL标签类
    compile 'jstl:jstl:1.2'
    //日志文件管理包
    compile 'log4j:log4j:1.2.17'
    //格式化对象
    compile 'com.alibaba:fastjson:1.1.41'
    compile 'org.slf4j:slf4j-api:1.7.7'
    compile 'org.slf4j:slf4j-log4j12:1.7.7'
    //映入JSON
    compile 'org.codehaus.jackson:jackson-mapper-asl:1.9.13'
    //上传组件包
    compile 'commons-fileupload:commons-fileupload:1.3.1'
    compile 'commons-io:commons-io:2.4'
    compile 'commons-codec:commons-codec:1.9'
    //
    testCompile group: 'junit', name: 'junit', version: '4.11'
    //generator-mybatis生成代码依赖
    mybatisGenerator 'org.mybatis.generator:mybatis-generator-core:1.3.2'
    mybatisGenerator 'tk.mybatis:mapper:3.3.2'
}
//配置generator的mysql配置文件
def getDbProperties = {
    def properties = new Properties()
    file("src/main/resources/generator/generator.properties").withInputStream { inputStream ->
        properties.load(inputStream)
    }
    properties;
}
task mybatisGenerate << {
    def properties = getDbProperties()
    ant.properties['targetProject'] = projectDir.path
    ant.properties['driverClass'] = properties.getProperty("jdbc.driverClass")
    ant.properties['connectionURL'] = properties.getProperty("jdbc.connectionURL")
    ant.properties['userId'] = properties.getProperty("jdbc.userId")
    ant.properties['password'] = properties.getProperty("jdbc.password")
    ant.properties['src_main_java'] = sourceSets.main.java.srcDirs[0].path
    ant.properties['src_main_resources'] = sourceSets.main.resources.srcDirs[0].path
    ant.properties['modelPackage'] = this.modelPackage
    ant.properties['mapperPackage'] = this.mapperPackage
    ant.properties['sqlMapperPackage'] = this.sqlMapperPackage
    ant.taskdef(
            name: 'mbgenerator',
            classname: 'org.mybatis.generator.ant.GeneratorAntTask',
            classpath: configurations.mybatisGenerator.asPath
    )
    ant.mbgenerator(overwrite: true,
            configfile: 'db/generatorConfig.xml', verbose: true) {
        propertyset {
            propertyref(name: 'targetProject')
            propertyref(name: 'userId')
            propertyref(name: 'driverClass')
            propertyref(name: 'connectionURL')
            propertyref(name: 'password')
            propertyref(name: 'src_main_java')
            propertyref(name: 'src_main_resources')
            propertyref(name: 'modelPackage')
            propertyref(name: 'mapperPackage')
            propertyref(name: 'sqlMapperPackage')
        }
    }
}

